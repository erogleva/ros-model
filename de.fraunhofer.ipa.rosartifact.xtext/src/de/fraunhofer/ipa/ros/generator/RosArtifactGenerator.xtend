/*
 * generated by Xtext 2.12.0
 */
package de.fraunhofer.ipa.ros.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import ros.*
/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class RosArtifactGenerator extends AbstractGenerator {



	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		for (node : resource.allContents.toIterable.filter(Node)){
			fsa.generateFile(node.getName()+".cpp",node.compile)
			}
		}

def compile(Node node) '''
#include <ros/ros.h>
            «FOR pub : node.publisher»
#include <«pub.message.package.name»/«pub.message.name».h>
            «ENDFOR»
            «FOR sub : node.subscriber»
#include <«sub.message.package.name»/«sub.message.name».h>
            «ENDFOR»
            «FOR srvserver : node.serviceserver»
#include <«srvserver.service.package.name»/«srvserver.service.name».h>
            «ENDFOR»
            «FOR srvclient : node.serviceclient»
#include <«srvclient.service.package.name»/«srvclient.service.name».h>
            «ENDFOR»

            «FOR srvserver : node.serviceserver»
bool  «srvserver.name»_cb («srvserver.service.package.name»::«srvserver.service.name» &req,
							«srvserver.service.package.name»::«srvserver.service.name» &res){
  return true;
}
            «ENDFOR»
            «FOR sub : node.subscriber»
void  «sub.name»_cb (const «sub.message.package.name»::«sub.message.name» msg){}
            «ENDFOR»


int main(int argc, char **argv)
{
  ros::init(argc, argv, "«node.name»");
  ros::NodeHandle n;
  
            «FOR pub : node.publisher»
  «pub.compile»
            «ENDFOR»
            «FOR sub : node.subscriber»
  «sub.compile»
            «ENDFOR»
            «FOR srvserver : node.serviceserver»
  «srvserver.compile»
            «ENDFOR»
            «FOR srvclient : node.serviceclient»
  «srvclient.compile»
            «ENDFOR»
            
  ros::spin();

  return 0;
}
            '''
            
def compile(Publisher pub)       
'''
ros::Publisher «pub.name»_pub = n.advertise<«pub.message.package.name»::«pub.message.name»>("«pub.name»", 1000);
'''
def compile(Subscriber sub)       
'''
ros::Subscriber «sub.name» = n.subscribe("«sub.name»", 1000, «sub.name»_cb);
'''
def compile(ServiceServer srvserver)       
'''
ros::ServiceServer «srvserver.name» = n.advertiseService("«srvserver.name»", «srvserver.name»_cb);
'''
def compile(ServiceClient srvclient)       
'''
ros::ServiceClient «srvclient.name» = n.serviceClient<«srvclient.service.package.name»::«srvclient.service.name»>("«srvclient.name»");
'''

}
